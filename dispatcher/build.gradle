plugins {
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.1.4-2'
    id 'org.jetbrains.dokka' version '0.9.15'
    id 'com.bmuschko.nexus' version '2.3.1'
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.4-2'
    compile 'com.squareup.okhttp3:mockwebserver:3.8.1'
    compile 'pl.droidsonroids.yaml:snakeyaml:1.18-android'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile('com.nhaarman:mockito-kotlin-kt1.1:1.5.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'junit:junit:4.12'
}

jacoco {
    toolVersion '0.7.9'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

group = POM_GROUP
version = POM_VERSION

modifyPom {
    project {
        artifactId POM_ARTIFACT_ID
        name POM_NAME
        description POM_DESCRIPTION
        url POM_URL

        scm {
            connection POM_SCM_CONNECTION_URL
            developerConnection POM_SCM_CONNECTION_URL
            url POM_URL
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'mszydlow'
                name 'Michał Szydłowski'
                email 'michal.szydlowski@droidsonroids.pl'
                roles {
                    role 'developer'
                }
            }
            developer {
                id 'koral'
                name 'Karol Wrótniak'
                email 'karol.wrotniak@droidsonroids.pl'
                roles {
                    role 'developer'
                }
            }
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    includes = ['extra.md']
    linkMapping {
        dir = sourceSets.main.kotlin.dirs.singleFile
        url = POM_URL
        suffix = '#L'
    }
}

javadoc.dependsOn dokka
